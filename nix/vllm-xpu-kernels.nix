{
  lib,
  src,
  version,
  cutlass-src,
  onednn-src,
  python3Packages,
  cmake,
  ninja,
  git,
  autoPatchelfHook,
  stdenv,
  intel-oneapi-base,
  intel-pti,
  torch-xpu,
  level-zero,
  intel-compute-runtime,
  intel-graphics-compiler,
  ocl-icd,
  zlib,
  which,
  ccache,
  pname ? "vllm-xpu-kernels",
  attn-kernels-xe-2,
  gdn-attn-kernels-xe-2,
  mqa-logits-kernels-xe-2,
  mhc-kernels-xe-2,
  grouped-gemm-xe-2,
  grouped-gemm-xe-default,
  # SYCL AOT target list. Exported as VLLM_XPU_AOT_DEVICES /
  # VLLM_XPU_XE2_AOT_DEVICES (upstream's CMakeLists honours both at
  # ~line 186).
  #   [] (default) -> empty-string export; upstream skips AOT and
  #     ships SPIR-V for IGC to specialize at first dispatch.
  #   [ "bmg" ...] -> AOT for the listed devices. Each entry adds
  #     one ocloc invocation at link time, so multi-device builds
  #     get expensive fast.
  aotDevices ? [ ],
  # Same toggle as vllm-xpu-lib.nix. Upstream setup.py auto-detects
  # ccache via `which("ccache")`, so having ccache in nativeBuildInputs
  # is enough to flip on -DCMAKE_{C,CXX}_COMPILER_LAUNCHER=ccache.
  useCcache ? true,
  # Build only one native-glue component. These values are forwarded as
  # setup.py/CMake feature-option environment variables. The default empty set
  # preserves upstream's all-enabled behavior for callers outside the split
  # factory.
  featureOptions ? { },
  withAttnLibrary ? true,
  withGdnAttnLibrary ? true,
  withMqaLogitsLibrary ? true,
  withMhcLibrary ? true,
  withGroupedGemmXe2Library ? true,
  withGroupedGemmXeDefaultLibrary ? true,
  pythonImportsCheck ? [ "vllm_xpu_kernels" ],
}:

let
  syclHome = "${intel-oneapi-base}/compiler/latest";
  aotDevicesStr = lib.concatStringsSep "," aotDevices;

  # See vllm-xpu-lib.nix for the full rationale on these values and
  # why they live on the derivation (rather than impureEnvVars).
  ccacheEnvAttrs = lib.optionalAttrs useCcache {
    CCACHE_DIR = "/var/cache/ccache";
    CCACHE_COMPRESS = "1";
    CCACHE_SLOPPINESS = "random_seed,time_macros,include_file_mtime,include_file_ctime,pch_defines";
    CCACHE_NOHASHDIR = "1";
    CCACHE_UMASK = "007";
  };

  ccachePreBuild = lib.optionalString useCcache ''
    export CCACHE_BASEDIR=$NIX_BUILD_TOP
  '';

  featureEnvAttrs = lib.mapAttrs (_name: enabled: if enabled then "ON" else "OFF") featureOptions;
in
python3Packages.buildPythonPackage (
  {
    inherit pname version;
    format = "pyproject";

    inherit src;

  nativeBuildInputs =
  [
    (python3Packages.writeShellScriptBin "cmake" ''
      exec ${python3Packages.cmake}/bin/cmake \
        -DFETCHCONTENT_SOURCE_DIR_onednn=${onednn-src} \
        "$@"
    '')
    ninja
    git
    autoPatchelfHook
    which
    python3Packages.setuptools
    python3Packages.setuptools-scm
    python3Packages.wheel
    python3Packages.packaging
    python3Packages.jinja2
    python3Packages.regex
    python3Packages.psutil
    python3Packages.cmake
    python3Packages.ninja
  ]
  ++ lib.optional useCcache ccache;

    buildInputs = [
      stdenv.cc.cc.lib
      intel-oneapi-base
      intel-pti
      level-zero
      intel-compute-runtime
      intel-graphics-compiler
      ocl-icd
      zlib
    ]
    ++ lib.optional withAttnLibrary attn-kernels-xe-2
    ++ lib.optional withGdnAttnLibrary gdn-attn-kernels-xe-2
    ++ lib.optional withMqaLogitsLibrary mqa-logits-kernels-xe-2
    ++ lib.optional withMhcLibrary mhc-kernels-xe-2
    ++ lib.optional withGroupedGemmXe2Library grouped-gemm-xe-2
    ++ lib.optional withGroupedGemmXeDefaultLibrary grouped-gemm-xe-default;

    propagatedBuildInputs = [
      torch-xpu
    ];

    dontUseCmakeConfigure = true;

    patches = [
    #  ./patches/0001-split-kernel-libs.patch
    #  ./patches/0004-skip-prebuilt-additional-libs.patch
    #  ./patches/0006-forward-mhc-feature-flag.patch
       ./patches/0007-fix-sourcepath-oneDNN.patch
    ];

    postPatch = ''
      # nixpkgs' setuptools is newer than the kernels' conservative build-only
      # upper bound; the stable torch and runtime pins remain exact.
      substituteInPlace pyproject.toml \
        --replace 'setuptools>=77.0.3,<80.0.0' 'setuptools'
    '';

    preBuild = ''
      ${ccachePreBuild}
      mkdir -p $TMPDIR/bin
      ln -sf ${intel-compute-runtime}/bin/ocloc $TMPDIR/bin/ocloc
      export PATH=$TMPDIR/bin:${syclHome}/bin:$PATH
      export LD_LIBRARY_PATH=${intel-graphics-compiler}/lib:${intel-compute-runtime}/lib:$LD_LIBRARY_PATH
      export SYCL_HOME=${syclHome}
      export CMPLR_ROOT=${syclHome}
      export MKLROOT=${intel-oneapi-base}/mkl/latest
      export CC=${syclHome}/bin/icx
      export CXX=${syclHome}/bin/icpx
      icpxToolchainFlags="--gcc-toolchain=${stdenv.cc.cc} -B${stdenv.cc.libc}/lib -L${stdenv.cc.libc}/lib -L${stdenv.cc.cc.lib}/lib -idirafter ${stdenv.cc.libc.dev}/include"
      export CFLAGS="$icpxToolchainFlags $CFLAGS"
      export CXXFLAGS="$icpxToolchainFlags $CXXFLAGS"
      export LDFLAGS="-L${stdenv.cc.libc}/lib -L${stdenv.cc.cc.lib}/lib $LDFLAGS"
      export LIBRARY_PATH=${stdenv.cc.libc}/lib:${stdenv.cc.cc.lib}/lib:${level-zero}/lib:$LIBRARY_PATH
      export CPATH=${stdenv.cc.libc.dev}/include:${level-zero}/include:$CPATH
      export CMAKE_PREFIX_PATH=${intel-oneapi-base}:$CMAKE_PREFIX_PATH
      export VLLM_CUTLASS_SRC_DIR=${cutlass-src}
      export CMAKE_ARGS="-DFETCHCONTENT_SOURCE_DIR_ONEDNN=${onednn-src}"
      export VLLM_XPU_AOT_DEVICES="${aotDevicesStr}"
      export VLLM_XPU_XE2_AOT_DEVICES="${aotDevicesStr}"
      export CMAKE_BUILD_TYPE=Release

      ${lib.optionalString withAttnLibrary "export VLLM_XPU_PREBUILT_ATTN_KERNELS_XE_2_LIB=${attn-kernels-xe-2}/lib/libattn_kernels_xe_2.so"}
      ${lib.optionalString withGdnAttnLibrary "export VLLM_XPU_PREBUILT_GDN_ATTN_KERNELS_XE_2_LIB=${gdn-attn-kernels-xe-2}/lib/libgdn_attn_kernels_xe_2.so"}
      ${lib.optionalString withMqaLogitsLibrary "export VLLM_XPU_PREBUILT_MQA_LOGITS_KERNELS_XE_2_LIB=${mqa-logits-kernels-xe-2}/lib/libmqa_logits_kernels_xe_2.so"}
      ${lib.optionalString withMhcLibrary "export VLLM_XPU_PREBUILT_MHC_KERNELS_XE_2_LIB=${mhc-kernels-xe-2}/lib/libmhc_kernels_xe_2.so"}
      ${lib.optionalString withGroupedGemmXe2Library "export VLLM_XPU_PREBUILT_GROUPED_GEMM_XE_2_LIB=${grouped-gemm-xe-2}/lib/libgrouped_gemm_xe_2.so"}
      ${lib.optionalString withGroupedGemmXeDefaultLibrary "export VLLM_XPU_PREBUILT_GROUPED_GEMM_XE_DEFAULT_LIB=${grouped-gemm-xe-default}/lib/libgrouped_gemm_xe_default.so"}

      export MAX_JOBS=''${NIX_BUILD_CORES:-1}
    '';

    autoPatchelfIgnoreMissingDeps = [
      "libcuda.so.1"
    ];

    dontCheckRuntimeDeps = true;
    dontStrip = true;

    inherit pythonImportsCheck;

    meta = {
      description = "vLLM XPU kernels (SYCL/CUTLASS-SYCL) for Intel Arc / Battlemage / PVC";
      homepage = "https://github.com/vllm-project/vllm-xpu-kernels";
      license = lib.licenses.asl20;
      platforms = [ "x86_64-linux" ];
    };
  }
  // ccacheEnvAttrs
  // featureEnvAttrs
)
